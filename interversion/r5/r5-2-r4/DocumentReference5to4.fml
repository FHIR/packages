/// url = 'http://hl7.org/fhir/StructureMap/DocumentReference5to4'
/// name = 'DocumentReference5to4'
/// title = 'FML Conversion for DocumentReference: R5 to R4'

// GG 8-Mar 2023: All OK

conceptmap "DocumentReferenceStatus" {
  prefix s = "http://hl7.org/fhir/document-reference-status"
  prefix t = "http://hl7.org/fhir/4.0/document-reference-status"

  s:"current" - t:"current"
  s:"superseded" - t:"superseded"
  s:"entered-in-error" - t:"entered-in-error"
}

conceptmap "ReferredDocumentStatus" {
  prefix s = "http://hl7.org/fhir/composition-status"
  prefix t = "http://hl7.org/fhir/4.0/composition-status"

//  s:"registered" - t:"registered"
//  s:"partial" - t:"partial"
  s:"preliminary" - t:"preliminary"
  s:"final" - t:"final"
  s:"amended" - t:"amended"
//  s:"corrected" - t:"corrected"
//  s:"appended" - t:"appended"
//  s:"cancelled" - t:"cancelled"
  s:"entered-in-error" - t:"entered-in-error"
//  s:"deprecated" - t:"deprecated"
//  s:"unknown" - t:"unknown"
}

uses "http://hl7.org/fhir/StructureDefinition/DocumentReference" alias DocumentReferenceR5 as source
uses "http://hl7.org/fhir/4.0/StructureDefinition/DocumentReference" alias DocumentReferenceR4 as target

imports "http://hl7.org/fhir/StructureMap/*5to4"

group DocumentReference(source src : DocumentReferenceR5, target tgt : DocumentReferenceR4) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
//  src.version -> tgt.version;
//  src.basedOn -> tgt.basedOn;
  src.status as v -> tgt.status = translate(v, '#DocumentReferenceStatus', 'code');
  src.docStatus as v -> tgt.docStatus = translate(v, '#ReferredDocumentStatus', 'code');
//  src.modality -> tgt.modality;
  src.type -> tgt.type;
  src.category -> tgt.category;
  src.subject -> tgt.subject;
  src as sn -> tgt.context as t then {
    src.event as s -> tgt then {
      s.concept -> t.event;
    } "event";
    src.facilityType -> t.facilityType;
    src.practiceSetting -> t.practiceSetting;
    src.period -> t.period;
  } "context";
//  src.bodySite -> tgt.bodySite;
  src.date -> tgt.date;
  src.author -> tgt.author;
  src.attester as s where src.attester.mode = 'professional' -> tgt.authenticator = s.party;
  src.custodian -> tgt.custodian;
  src.relatesTo as s -> tgt.relatesTo as t then DocumentReferenceRelatesTo(s,t);
  src.description -> tgt.description;
  src.securityLabel -> tgt.securityLabel;
  src.content as s -> tgt.content as t then DocumentReferenceContent(s,t);
}

group DocumentReferenceRelatesTo(source src, target tgt) extends BackboneElement {
  src.code as s -> tgt then  {
    s.coding as c where c.system = 'http://hl7.org/fhir/document-relationship-type' -> tgt.code = c.code;
  };
  src.target -> tgt.target;
}

group DocumentReferenceContent(source src, target tgt) extends BackboneElement {
  src.attachment -> tgt.attachment;
  src.profile as s -> tgt then {
    s.value : Coding -> tgt.format;
  };
}

