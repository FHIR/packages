/// url = 'http://hl7.org/fhir/StructureMap/TriggerDefinition4Bto5'
/// name = 'TriggerDefinition4Bto5'
/// title = 'FML Conversion for TriggerDefinition: R4B to R5'

uses "http://hl7.org/fhir/4.3/StructureDefinition/TriggerDefinition" alias TriggerDefinitionR4B as source
uses "http://hl7.org/fhir/StructureDefinition/TriggerDefinition" alias TriggerDefinitionR5 as target

imports "http://hl7.org/fhir/StructureMap/*4Bto5"

conceptmap "TriggerType" {
  prefix s = "http://hl7.org/fhir/4.3/trigger-type"
  prefix t = "http://hl7.org/fhir/trigger-type"

  s:named-event - t:named-event
  s:periodic - t:periodic
  s:data-changed - t:data-changed
  s:data-accessed - t:data-accessed
  s:data-access-ended - t:data-access-ended
}

group TriggerDefinition(source src : TriggerDefinitionR4B, target tgt : TriggerDefinitionR5) extends DataType <<type+>> {
  src.type as v -> tgt.type = translate(v, '#TriggerType', 'c');
  src.name -> tgt.name;
  src.code -> tgt.code;
  src.subscriptionTopic -> tgt.subscriptionTopic;
  src.timing : Timing -> tgt.timing;
  src.timing : Reference -> tgt.timing;
  src.timing : date -> tgt.timing;
  src.timing : dateTime -> tgt.timing;
  src.data -> tgt.data;
  src.condition -> tgt.condition;
}

