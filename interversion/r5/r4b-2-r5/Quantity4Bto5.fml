/// url = 'http://hl7.org/fhir/StructureMap/Quantity4Bto5'
/// name = 'Quantity4Bto5'
/// title = 'FML Conversion for Quantity: R4B to R5'

// GG 7-Mar 2023: All OK

conceptmap "QuantityComparator" {
  prefix s = "http://hl7.org/fhir/4.3/quantity-comparator"
  prefix t = "http://hl7.org/fhir/quantity-comparator"

  s:"<" - t:"<"
  s:"<=" - t:"<="
  s:">=" - t:">="
  s:">" - t:">"
//  s:"ad" - t:"ad" - can't round trip this without an extension
}

uses "http://hl7.org/fhir/4.3/StructureDefinition/Quantity" alias QuantityR4B as source
uses "http://hl7.org/fhir/StructureDefinition/Quantity" alias QuantityR5 as target

imports "http://hl7.org/fhir/StructureMap/*4Bto5"

group Quantity(source src : QuantityR4B, target tgt : QuantityR5) extends Element <<type+>> {
  src.value -> tgt.value;
  src.comparator as v -> tgt.comparator = translate(v, '#QuantityComparator', 'code');
  src.unit -> tgt.unit;
  src.system -> tgt.system;
  src.code -> tgt.code;
}

