/// url = 'http://hl7.org/fhir/StructureMap/Location4Bto5'
/// name = 'Location4Bto5'
/// title = 'FML Conversion for Location: R4B to R5'

// RJE 9-Mar 2023: All OK

conceptmap "LocationMode" {
  prefix s = "http://hl7.org/fhir/4.3/location-mode"
  prefix t = "http://hl7.org/fhir/location-mode"

  s:"instance" - t:"instance"
  s:"kind" - t:"kind"
}

conceptmap "LocationStatus" {
  prefix s = "http://hl7.org/fhir/4.3/location-status"
  prefix t = "http://hl7.org/fhir/location-status"

  s:"active" - t:"active"
  s:"suspended" - t:"suspended"
  s:"inactive" - t:"inactive"
}

uses "http://hl7.org/fhir/4.3/StructureDefinition/Location" alias LocationR4B as source
uses "http://hl7.org/fhir/StructureDefinition/Location" alias LocationR5 as target

imports "http://hl7.org/fhir/StructureMap/*4Bto5"

group Location(source src : LocationR4B, target tgt : LocationR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status as v -> tgt.status = translate(v, '#LocationStatus', 'code');
  src.operationalStatus -> tgt.operationalStatus;
  src.name -> tgt.name;
  src.alias -> tgt.alias;
  src.description -> tgt.description;
  src.mode as v -> tgt.mode = translate(v, '#LocationMode', 'code');
  src.type -> tgt.type;
  src.telecom as s -> tgt.contact = create('ExtendedContactDetail') as t, t.telecom = s.value;
  src.address -> tgt.address;
  src.physicalType -> tgt.form;
  src.position as s -> tgt.position as t then LocationPosition(s,t);
  src.managingOrganization -> tgt.managingOrganization;
  src.partOf -> tgt.partOf;
//  src.characteristic -> tgt.characteristic;
  src.hoursOfOperation as s -> tgt.hoursOfOperation = create('Availability') as t, t.availableTime = create('Element') as te, te.daysOfWeek = s.daysOfWeek, te.allDay = s.allDay, te.availableStartTime = s.openingTime, te.availableEndTime = s.closingTime;
//  src.virtualService -> tgt.virtualService;
  src.endpoint -> tgt.endpoint;
}

group LocationPosition(source src, target tgt) extends BackboneElement {
  src.longitude -> tgt.longitude;
  src.latitude -> tgt.latitude;
  src.altitude -> tgt.altitude;
}

