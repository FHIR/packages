/// url = 'http://hl7.org/fhir/StructureMap/Linkage4Bto5'
/// name = 'Linkage4Bto5'
/// title = 'FML Conversion for Linkage: R4B to R5'

conceptmap "LinkageType" {
  prefix s = "http://hl7.org/fhir/4.3/linkage-type"
  prefix t = "http://hl7.org/fhir/linkage-type"

  s:"source" - t:"source"
  s:"alternate" - t:"alternate"
  s:"historical" - t:"historical"
}

uses "http://hl7.org/fhir/4.3/StructureDefinition/Linkage" alias LinkageR4B as source
uses "http://hl7.org/fhir/StructureDefinition/Linkage" alias LinkageR5 as target

imports "http://hl7.org/fhir/StructureMap/*4Bto5"

group Linkage(source src : LinkageR4B, target tgt : LinkageR5) extends DomainResource <<type+>> {
  src.active -> tgt.active;
  src.author -> tgt.author;
  src.item as s -> tgt.item as t then LinkageItem(s,t);
}

group LinkageItem(source src, target tgt) extends BackboneElement {
  src.type as v -> tgt.type = translate(v, '#LinkageType', 'code');
  src.resource -> tgt.resource;
}

