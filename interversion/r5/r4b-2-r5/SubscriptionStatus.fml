/// url = 'http://hl7.org/fhir/StructureMap/SubscriptionStatus4Bto5'
/// name = 'SubscriptionStatus4Bto5'
/// title = 'FML Conversion for SubscriptionStatus: R4B to R5'

conceptmap "SubscriptionNotificationType" {
  prefix s = "http://hl7.org/fhir/4.3/subscription-notification-type"
  prefix t = "http://hl7.org/fhir/subscription-notification-type"

  s:"handshake" - t:"handshake"
  s:"heartbeat" - t:"heartbeat"
  s:"event-notification" - t:"event-notification"
  s:"query-status" - t:"query-status"
  s:"query-event" - t:"query-event"
}

conceptmap "SubscriptionStatus" {
  prefix s = "http://hl7.org/fhir/4.3/subscription-status"
  prefix t = "http://hl7.org/fhir/subscription-status"

  s:"requested" - t:"requested"
  s:"active" - t:"active"
  s:"error" - t:"error"
  s:"off" - t:"off"
  s:"entered-in-error" - t:"entered-in-error"
}

uses "http://hl7.org/fhir/4.3/StructureDefinition/SubscriptionStatus" alias SubscriptionStatusR4B as source
uses "http://hl7.org/fhir/StructureDefinition/SubscriptionStatus" alias SubscriptionStatusR5 as target

imports "http://hl7.org/fhir/StructureMap/*4Bto5"

group SubscriptionStatus(source src : SubscriptionStatusR4B, target tgt : SubscriptionStatusR5) extends DomainResource <<type+>> {
  src.status as v -> tgt.status = translate(v, '#SubscriptionStatus', 'code');
  src.type as v -> tgt.type = translate(v, '#SubscriptionNotificationType', 'code');
  src.eventsSinceSubscriptionStart -> tgt.eventsSinceSubscriptionStart;
  src.notificationEvent as s -> tgt.notificationEvent as t then SubscriptionStatusNotificationEvent(s,t);
  src.subscription -> tgt.subscription;
  src.topic -> tgt.topic;
  src.error -> tgt.error;
}

group SubscriptionStatusNotificationEvent(source src, target tgt) extends BackboneElement {
  src.eventNumber -> tgt.eventNumber;
  src.timestamp -> tgt.timestamp;
  src.focus -> tgt.focus;
  src.additionalContext -> tgt.additionalContext;
}

