/// url = 'http://hl7.org/fhir/StructureMap/List5to4B'
/// name = 'List5to4B'
/// title = 'FML Conversion for List: R5 to R4B'

uses "http://hl7.org/fhir/StructureDefinition/List" alias ListR5 as source
uses "http://hl7.org/fhir/4.3/StructureDefinition/List" alias ListR4B as target

imports "http://hl7.org/fhir/StructureMap/*5to4B"

conceptmap "ListMode" {
  prefix s = "http://hl7.org/fhir/list-mode"
  prefix t = "http://hl7.org/fhir/4.3/list-mode"

  s:working - t:working
  s:snapshot - t:snapshot
  s:changes - t:changes
}

conceptmap "ListStatus" {
  prefix s = "http://hl7.org/fhir/list-status"
  prefix t = "http://hl7.org/fhir/4.3/list-status"

  s:current - t:current
  s:retired - t:retired
  s:entered-in-error - t:entered-in-error
}

group List(source src : ListR5, target tgt : ListR4B) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.status as v -> tgt.status = translate(v, '#ListStatus', 'c');
  src.mode as v -> tgt.mode = translate(v, '#ListMode', 'c');
  src.title -> tgt.title;
  src.code -> tgt.code;
  src.subject -> tgt.subject;
  src.encounter -> tgt.encounter;
  src.date -> tgt.date;
  src.source -> tgt.source;
  src.orderedBy -> tgt.orderedBy;
  src.note -> tgt.note;
  src.entry as s -> tgt.entry as t then ListEntry(s,t);
  src.emptyReason -> tgt.emptyReason;
}

group ListEntry(source src, target tgt) extends BackboneElement {
  src.flag -> tgt.flag;
  src.deleted -> tgt.deleted;
  src.date -> tgt.date;
  src.item -> tgt.item;
}

