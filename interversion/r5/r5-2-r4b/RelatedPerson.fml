/// url = 'http://hl7.org/fhir/StructureMap/RelatedPerson5to4B'
/// name = 'RelatedPerson5to4B'
/// title = 'FML Conversion for RelatedPerson: R5 to R4B'

uses "http://hl7.org/fhir/StructureDefinition/RelatedPerson" alias RelatedPersonR5 as source
uses "http://hl7.org/fhir/4.3/StructureDefinition/RelatedPerson" alias RelatedPersonR4B as target

imports "http://hl7.org/fhir/StructureMap/*5to4B"

conceptmap "AdministrativeGender" {
  prefix s = "http://hl7.org/fhir/administrative-gender"
  prefix t = "http://hl7.org/fhir/4.3/administrative-gender"

  s:male - t:male
  s:female - t:female
  s:other - t:other
  s:unknown - t:unknown
}

group RelatedPerson(source src : RelatedPersonR5, target tgt : RelatedPersonR4B) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.active -> tgt.active;
  src.patient -> tgt.patient;
  src.relationship -> tgt.relationship;
  src.name -> tgt.name;
  src.telecom -> tgt.telecom;
  src.gender as v -> tgt.gender = translate(v, '#AdministrativeGender', 'c');
  src.birthDate -> tgt.birthDate;
  src.address -> tgt.address;
  src.photo -> tgt.photo;
  src.period -> tgt.period;
  src.communication as s -> tgt.communication as t then RelatedPersonCommunication(s,t);
}

group RelatedPersonCommunication(source src, target tgt) extends BackboneElement {
  src.language -> tgt.language;
  src.preferred -> tgt.preferred;
}

