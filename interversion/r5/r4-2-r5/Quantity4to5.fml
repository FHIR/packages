/// url = 'http://hl7.org/fhir/StructureMap/Quantity4to5'
/// name = 'Quantity4to5'
/// title = 'FML Conversion for Quantity: R4 to R5'

conceptmap "QuantityComparator" {
  prefix s = "http://hl7.org/fhir/4.0/quantity-comparator"
  prefix t = "http://hl7.org/fhir/quantity-comparator"

  s:"<" - t:"<"
  s:"<=" - t:"<="
  s:">=" - t:">="
  s:">" - t:">"
  s:"ad" - t:"ad"
}

uses "http://hl7.org/fhir/4.0/StructureDefinition/Quantity" alias QuantityR4 as source
uses "http://hl7.org/fhir/StructureDefinition/Quantity" alias QuantityR5 as target

imports "http://hl7.org/fhir/StructureMap/*4to5"

group Quantity(source src : QuantityR4, target tgt : QuantityR5) extends DataType <<type+>> {
  src.value -> tgt.value;
  src.comparator as v -> tgt.comparator = translate(v, '#QuantityComparator', 'code');
  src.unit -> tgt.unit;
  src.system -> tgt.system;
  src.code -> tgt.code;
}

