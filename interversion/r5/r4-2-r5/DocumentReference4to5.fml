/// url = 'http://hl7.org/fhir/StructureMap/DocumentReference4to5'
/// name = 'DocumentReference4to5'
/// title = 'FML Conversion for DocumentReference: R4 to R5'

// GG 8-Mar 2023: All OK

conceptmap "DocumentReferenceStatus" {
  prefix s = "http://hl7.org/fhir/4.0/document-reference-status"
  prefix t = "http://hl7.org/fhir/document-reference-status"

  s:"current" - t:"current"
  s:"superseded" - t:"superseded"
  s:"entered-in-error" - t:"entered-in-error"
}

conceptmap "ReferredDocumentStatus" {
  prefix s = "http://hl7.org/fhir/4.0/composition-status"
  prefix t = "http://hl7.org/fhir/composition-status"

//  s:"registered" - t:"registered"
//  s:"partial" - t:"partial"
  s:"preliminary" - t:"preliminary"
  s:"final" - t:"final"
  s:"amended" - t:"amended"
//  s:"corrected" - t:"corrected"
//  s:"appended" - t:"appended"
//  s:"cancelled" - t:"cancelled"
  s:"entered-in-error" - t:"entered-in-error"
//  s:"deprecated" - t:"deprecated"
//  s:"unknown" - t:"unknown"
}

uses "http://hl7.org/fhir/4.0/StructureDefinition/DocumentReference" alias DocumentReferenceR4 as source
uses "http://hl7.org/fhir/StructureDefinition/DocumentReference" alias DocumentReferenceR5 as target

imports "http://hl7.org/fhir/StructureMap/*4to5"

group DocumentReference(source src : DocumentReferenceR4, target tgt : DocumentReferenceR5) extends DomainResource <<type+>> {
  src.masterIdentifier -> tgt.identifier;
  src.identifier -> tgt.identifier;
//  src.version -> tgt.version;
//  src.basedOn -> tgt.basedOn;
  src.status as v -> tgt.status = translate(v, '#DocumentReferenceStatus', 'code');
  src.docStatus as v -> tgt.docStatus = translate(v, '#ReferredDocumentStatus', 'code');
//  src.modality -> tgt.modality;
  src.type -> tgt.type;
  src.category -> tgt.category;
  src.subject -> tgt.subject;
  src.context as s -> tgt then {
    s.encounter -> tgt.context;
    s.event as s -> tgt.event as t, t.concept = s;
    s.period -> tgt.period;
    s.facilityType -> tgt.facilityType;
    s.practiceSetting -> tgt.practiceSetting;
  };
//  src.bodySite -> tgt.bodySite;
  src.date -> tgt.date;
  src.author -> tgt.author;
  src.authenticator as s -> tgt.attester as t, t.mode = 'professional', t.party = s;
  src.custodian -> tgt.custodian;
  src.relatesTo as s -> tgt.relatesTo as t then DocumentReferenceRelatesTo(s,t);
  src.description -> tgt.description;
  src.securityLabel -> tgt.securityLabel;
  src.content as s -> tgt.content as t then DocumentReferenceContent(s,t);
}

group DocumentReferenceRelatesTo(source src, target tgt) extends BackboneElement {
  src.code as s -> tgt.code as t then code2CodeableConcept(s, t, 'http://hl7.org/fhir/document-relationship-type');
  src.target -> tgt.target;
}

group DocumentReferenceContent(source src, target tgt) extends BackboneElement {
  src.attachment -> tgt.attachment;
  src.format as s -> tgt.profile as t, t.value = s;
}


