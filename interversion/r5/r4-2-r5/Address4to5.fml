/// url = 'http://hl7.org/fhir/StructureMap/Address4to5'
/// name = 'Address4to5'
/// title = 'FML Conversion for Address: R4 to R5'

// GG 7-Mar 2023: All OK

conceptmap "AddressType" {
  prefix s = "http://hl7.org/fhir/4.0/address-type"
  prefix t = "http://hl7.org/fhir/address-type"

  s:"postal" - t:"postal"
  s:"physical" - t:"physical"
  s:"both" - t:"both"
}

conceptmap "AddressUse" {
  prefix s = "http://hl7.org/fhir/4.0/address-use"
  prefix t = "http://hl7.org/fhir/address-use"

  s:"home" - t:"home"
  s:"work" - t:"work"
  s:"temp" - t:"temp"
  s:"old" - t:"old"
  s:"billing" - t:"billing"
}

uses "http://hl7.org/fhir/4.0/StructureDefinition/Address" alias AddressR4 as source
uses "http://hl7.org/fhir/StructureDefinition/Address" alias AddressR5 as target

imports "http://hl7.org/fhir/StructureMap/*4to5"

group Address(source src : AddressR4, target tgt : AddressR5) extends Element <<type+>> {
  src.use as v -> tgt.use = translate(v, '#AddressUse', 'code');
  src.type as v -> tgt.type = translate(v, '#AddressType', 'code');
  src.text -> tgt.text;
  src.line -> tgt.line;
  src.city -> tgt.city;
  src.district -> tgt.district;
  src.state -> tgt.state;
  src.postalCode -> tgt.postalCode;
  src.country -> tgt.country;
  src.period -> tgt.period;
}

