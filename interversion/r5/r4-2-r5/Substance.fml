/// url = 'http://hl7.org/fhir/StructureMap/Substance4to5'
/// name = 'Substance4to5'
/// title = 'FML Conversion for Substance: R4 to R5'

uses "http://hl7.org/fhir/4.0/StructureDefinition/Substance" alias SubstanceR4 as source
uses "http://hl7.org/fhir/StructureDefinition/Substance" alias SubstanceR5 as target

imports "http://hl7.org/fhir/StructureMap/*4to5"

conceptmap "FHIRSubstanceStatus" {
  prefix s = "http://hl7.org/fhir/4.0/substance-status"
  prefix t = "http://hl7.org/fhir/substance-status"

  s:active - t:active
  s:inactive - t:inactive
  s:entered-in-error - t:entered-in-error
}

group Substance(source src : SubstanceR4, target tgt : SubstanceR5) extends DomainResource <<type+>> {
  src.identifier -> tgt.identifier;
  src.instance -> tgt.instance;
  src.status as v -> tgt.status = translate(v, '#FHIRSubstanceStatus', 'c');
  src.category -> tgt.category;
  src.code -> tgt.code;
  src.description -> tgt.description;
  src.expiry -> tgt.expiry;
  src.quantity -> tgt.quantity;
  src.ingredient as s -> tgt.ingredient as t then SubstanceIngredient(s,t);
}

group SubstanceIngredient(source src, target tgt) extends BackboneElement {
  src.quantity -> tgt.quantity;
  src.substance : CodeableConcept -> tgt.substance;
  src.substance : Reference -> tgt.substance;
}

