/// url = 'http://hl7.org/fhir/StructureMap/ContactPoint4to5'
/// name = 'ContactPoint4to5'
/// title = 'FML Conversion for ContactPoint: R4 to R5'

uses "http://hl7.org/fhir/4.0/StructureDefinition/ContactPoint" alias ContactPointR4 as source
uses "http://hl7.org/fhir/StructureDefinition/ContactPoint" alias ContactPointR5 as target

imports "http://hl7.org/fhir/StructureMap/*4to5"

conceptmap "ContactPointSystem" {
  prefix s = "http://hl7.org/fhir/4.0/contact-point-system"
  prefix t = "http://hl7.org/fhir/contact-point-system"

  s:phone - t:phone
  s:fax - t:fax
  s:email - t:email
  s:pager - t:pager
  s:url - t:url
  s:sms - t:sms
  s:other - t:other
}

conceptmap "ContactPointUse" {
  prefix s = "http://hl7.org/fhir/4.0/contact-point-use"
  prefix t = "http://hl7.org/fhir/contact-point-use"

  s:home - t:home
  s:work - t:work
  s:temp - t:temp
  s:old - t:old
  s:mobile - t:mobile
}

group ContactPoint(source src : ContactPointR4, target tgt : ContactPointR5) extends DataType <<type+>> {
  src.system as v -> tgt.system = translate(v, '#ContactPointSystem', 'c');
  src.value -> tgt.value;
  src.use as v -> tgt.use = translate(v, '#ContactPointUse', 'c');
  src.rank -> tgt.rank;
  src.period -> tgt.period;
}

